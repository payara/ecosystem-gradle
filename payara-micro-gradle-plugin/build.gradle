plugins {
    id 'com.gradle.plugin-publish' version '0.18.0'
    id 'maven-publish'
    id 'java'
    id 'java-gradle-plugin'
    id 'com.github.spotbugs' version '5.0.14' // Add SpotBugs plugin
}

group = 'fish.payara.gradle.plugins'
version = '2.1.0'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api gradleApi()
    api localGroovy()
    implementation 'org.jdom:jdom2:2.0.6'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.10.0'
    testImplementation gradleTestKit()
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
}

// SpotBugs Configuration
spotbugs {
    toolVersion = '4.2.3'    // Use the latest stable version
    effort = 'max'            // Use maximum effort for more detailed analysis
    reportLevel = 'low'       // Report all issues (low, medium, high)
    ignoreFailures = true    // Set to true if you want the build to continue even if there are violations
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = true   // Enable XML report generation
        html.enabled = false  // Enable HTML report generation
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

pluginBundle {
    plugins {
        website = 'https://www.payara.fish'
        vcsUrl = 'https://github.com/payara/ecosystem-gradle.git'
        tags = ['payara', 'micro', 'javaee', 'microprofile', 'jakartaee']
        microPlugin {
            id = 'fish.payara.micro-gradle-plugin'
            displayName = 'Payara Micro Gradle Plugin'
            description = 'Payara Micro gradle plugin incorporates payara-micro with the produced artifact.'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "fish.payara.gradle.plugins"
            artifactId = "payara-micro-gradle-plugin"
            version = "2.1.0"

            from components.java
        }
    }
}
