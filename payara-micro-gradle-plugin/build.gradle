plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
}

apply plugin: 'java'
apply plugin: 'maven' 
apply plugin: "com.gradle.plugin-publish"

group = 'fish.payara.gradle.plugins'
version = '1.0.4-SNAPSHOT'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.apache.commons:commons-lang3:3.10'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.awaitility:awaitility:4.0.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

pluginBundle {
    plugins {
        website = 'https://www.payara.fish'
        vcsUrl = 'https://github.com/payara/ecosystem-gradle.git'
        tags = ['payara', 'micro', 'javaee', 'microprofile', 'jakartaee']
        microPlugin {
            id = 'fish.payara.micro-gradle-plugin'
            displayName = 'Payara Micro Gradle Plugin'
            description = 'Payara Micro gradle plugin incorporates payara-micro with the produced artifact.'
        }
    }
}
